AC_REVISION([$Id$])dnl

dnl Process this file with autoconf to produce a configure script.



AC_INIT
AC_CONFIG_SRCDIR([libecs/Entity.hpp])

source ../ecell_version.sh


AM_CONFIG_HEADER(ecell_config.h)

AM_INIT_AUTOMAKE([ecell],${ECELL_VERSION_NUMBER})

ECELL_DIRNAME=${PACKAGE}-${ECELL_MAJOR_VERSION}.${ECELL_MINOR_VERSION}

AC_DEFINE_UNQUOTED( ECELL_MAJOR_VERSION, ${ECELL_MAJOR_VERSION},[] )
AC_DEFINE_UNQUOTED( ECELL_MINOR_VERSION, ${ECELL_MINOR_VERSION},[] )
AC_DEFINE_UNQUOTED( ECELL_MICRO_VERSION, ${ECELL_MICRO_VERSION},[] )
AC_DEFINE_UNQUOTED( ECELL_VERSION_STRING, "${ECELL_VERSION_NUMBER}",[] )
AC_SUBST( ECELL_DIRNAME )
AC_SUBST( ECELL_MAJOR_VERSION )
AC_SUBST( ECELL_MINOR_VERSION )
AC_SUBST( ECELL_MICRO_VERSION )


AM_SANITY_CHECK

AC_CANONICAL_HOST

if test $prefix = "NONE" ; then
	PREFIX=$ac_default_prefix
else
	PREFIX=$prefix
fi

AC_SUBST(PREFIX)

dnl
AC_ARG_WITH(gtk,
  AC_HELP_STRING([--without-gtk],
	         [build ecell without gtk library.]),
  [ECELL_SUBDIRS="libecs libemc pyecs pyecell bin dm"],
  [ECELL_SUBDIRS="libecs libemc pyecs pyecell bin dm session-monitor model-editor toollauncher"]
)
AC_SUBST( ECELL_SUBDIRS )
dnl


dnl Checks for programs.

AC_PROG_CC
AC_PROG_CXX
AC_PROG_CPP
AC_PROG_CXXCPP
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_RANLIB

AM_DISABLE_STATIC
dnl AC_LIBLTDL_INSTALLABLE(../libltdl)
AC_LIBLTDL_CONVENIENCE(../libltdl)
AC_SUBST(INCLTDL)
AC_SUBST(LIBLTDL)
AC_LIBTOOL_DLOPEN
AC_PROG_LIBTOOL


rm -f conftest
./libtool --config > conftest
. ./conftest
rm -f conftest
dnl
dnl compatibility for libtool 1.5.6
LTDL_SHLIB_EXT=""
if test -n "$shrext_cmds"; then
    eval LTDL_SHLIB_EXT=\"$shrext_cmds\"
    AC_SUBST(LTDL_SHLIB_EXT)
dnl compatibility for libtool 1.5.0
elif test -n "$shrext"; then
    LTDL_SHLIB_EXT=$shrext
    AC_SUBST(LTDL_SHLIB_EXT)
dnl compatibility for libtool 1.4.x
else
  AC_CACHE_CHECK([which extension is used for shared libraries],
    libltdl_cv_shlibext, [dnl
  (
    last=
    for spec in $library_names_spec; do
      last="$spec"
    done
  changequote(, )
    echo "$last" | sed 's/\[.*\]//;s/^[^.]*//;s/\$.*$//;s/\.$//' > conftest
  changequote([, ])
  )
  libltdl_cv_shlibext=`cat conftest`
  rm -f conftest
  ])
  if test -n "$libltdl_cv_shlibext"; then
    LTDL_SHLIB_EXT=$libltdl_cv_shlibext
    AC_SUBST(LTDL_SHLIB_EXT)
  fi
fi



AM_PATH_PYTHON(2.2)
AC_SUBST(PYTHON)

AC_LANG([C++])

dnl Checks for libraries.

dnl assuming libc and libm always present...

AC_CHECK_LIB(m, sin, LIBM="-lm")
AC_SUBST(LIBM)
dnl
AC_CHECK_LIB(c, open, LIBC="-lc")
AC_SUBST(LIBC)
dnl

dnl disable checking dlopen for windows
case ${host_os} in
  *cygwin* | *mingw* | *win32*)
    os_win32=yes
    AM_CHECK_PYMOD(py2exe,,,[AC_MSG_ERROR([could not find py2exe module])])
    ;;
  *)
    AC_CHECK_LIB(dl, dlopen, LIBDL="-ldl")
    AC_SUBST(LIBDL) 
    os_win32=no
    ;;
esac
AM_CONDITIONAL(OS_WIN32, test "$os_win32" = "yes")


AC_CHECK_LIB(gslcblas,cblas_dgemm,,[AC_MSG_ERROR([The libgslcblas (part of GNU Scientific Library) could not be found.])])

AC_CHECK_LIB(gsl,gsl_block_alloc,,[AC_MSG_ERROR([The libgsl (GNU Scientific Library) could not be found.])])

AM_CHECK_PYMOD(numpy,,,[AC_MSG_ERROR([could not find Python numpy module])])
AM_CHECK_PYMOD(xml,,,[AC_MSG_ERROR([could not find Python xml module])])

if test -n "$with_gtk"; then
   AM_CHECK_PYMOD(pygtk,,,[AC_MSG_ERROR([could not find Python pygtk module])])
   AM_CHECK_PYMOD(gtk,,,[AC_MSG_ERROR([could not find Python gtk module])])
   AM_CHECK_PYMOD(gtk.gdk,,,
		[AC_MSG_ERROR([could not find Python gtk.gdk module])])
   AM_CHECK_PYMOD(gtk.glade,,,
		[AC_MSG_ERROR([could not find Python gtk.glade module])])
   AM_CHECK_PYMOD(gnome.ui,,[AC_SUBST([GNOME_INSTALLED],yes)],[AC_SUBST([GNOME_INSTALLED],no)])
   AM_CHECK_PYMOD(gnomecanvas,,,[AC_MSG_WARN(could not find gnomecanvas module. model-editor will not work. )])
   AM_CHECK_PYMOD(xml.dom.minidom,,,[AC_MSG_WARN(could not find xml.dom.minidom module. model-editor will not work.)])
fi


dnl Checks for header files.

AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h unistd.h)
dnl
dnl stl
dnl
dnl AC_CHECK_HEADER(stl.h,,
dnl 	AC_MSG_ERROR([stl.h not found.])
dnl )
AC_CHECK_HEADERS(math,ac_ch_math=yes,)
AC_CHECK_HEADERS(cmath,ac_ch_cmath=yes,)
if test "$ac_ch_cmath" == "yes"; then
    AC_MSG_CHECKING(for INFINITY)
    AC_CACHE_VAL(ac_cv_func_or_macro_infinity,
        [AC_TRY_LINK(
            [#include <cmath>],
            [double inf = INFINITY;],
            [ac_cv_func_or_macro_infinity=yes],
            [ac_cv_func_or_macro_infinity=no])])
    if [[ $ac_cv_func_or_macro_infinity == yes ]]; then
        AC_DEFINE(HAVE_INFINITY, 1, Define to 1 if INFINITY is supported.)
        AC_MSG_RESULT(yes)
    else
        AC_MSG_RESULT(no)
    fi
elif test "$ac_ch_math" == "yes"; then
    AC_MSG_CHECKING(for INFINITY)
    AC_CACHE_VAL(ac_cv_func_or_macro_infinity,
        [AC_TRY_LINK(
            [#include <cmath>],
            [double inf = INFINITY;],
            [ac_cv_func_or_macro_infinity=yes],
            [ac_cv_func_or_macro_infinity=no])])
    if [[ $ac_cv_func_or_macro_infinity == yes ]]; then
        AC_DEFINE(HAVE_INFINITY, 1, Define to 1 if INFINITY is supported.)
        AC_MSG_RESULT(yes)
    else
        AC_MSG_RESULT(no)
    fi
fi
AC_CHECK_HEADERS(windows.h,,)
AC_MSG_CHECKING(for __PRETTY_FUNCTION__)
AC_CACHE_VAL(ac_cv_macro_pf,
    [AC_TRY_LINK(
        [#include <stdio.h>],
        [const char* pf = __PRETTY_FUNCTION__;],
        [ac_cv_macro_pf=yes],
        [ac_cv_macro_pf=no])])
if [[ $ac_cv_macro_pf == yes ]]; then
    AC_DEFINE(HAVE_PRETTY_FUNCTION, 1, Define to 1 if __PRETTY_FUNCTION__ is supported.)
    AC_MSG_RESULT(yes)
else
    AC_MSG_RESULT(no)
fi
AC_ARG_WITH(msvc, [  --with-msvc             create dmtool_config.h 4 MSVC])
if test "${with_msvc}" == "yes"; then
    msvcFlag=1
    AC_DEFINE_UNQUOTED(USE_MSVC, ${msvcFlag}, The Flag of the Compiler)
fi


dnl
dnl
EXT_GUESS="../../../.. ../../.. ../.. ../ ./ $prefix /usr /usr/local /opt"
dnl
dnl
dnl dmtool package
dnl
if test "$DMTOOL_DIR" = "" ; then
        AC_MSG_CHECKING( for dmtool dir)
        ac_cv_dmtool_dir="no"
        for ac_dir in $EXT_GUESS ; do
                if test -d $ac_dir/dmtool ; then
                        ac_cv_dmtool_dir=`(cd $ac_dir ; pwd)`
			DMTOOL_DIR=$ac_cv_dmtool_dir
                fi
        done

	if test "$DMTOOL_DIR" = "" ; then	
		AC_MSG_ERROR("dmtool not found.")
	fi

        AC_MSG_RESULT($ac_cv_dmtool_dir)
        DMTOOL_DIR=$ac_cv_dmtool_dir
        DMTOOL_INCLUDE_DIR=$ac_cv_dmtool_dir
fi
AC_SUBST(DMTOOL_DIR)
AC_SUBST(DMTOOL_INCLUDE_DIR)


AM_CHECK_PYTHON_HEADERS(,[AC_MSG_ERROR([Failed to find Python headers.])])

dnl numpy package.
dnl find arrayobject.h.
dnl
py_prefix=`$PYTHON -c "import sys; print sys.prefix"`
pydir=python${PYTHON_VERSION}
numpy_include="site-packages/numpy/core/include"
EXT_GUESS="${py_prefix}/Lib/${numpy_include} ${py_prefix}/lib/${pydir}/${numpy_include} ${py_prefix}/lib64/${pydir}/${numpy_include} /usr/lib/${pydir}/${numpy_include} /usr/lib64/${pydir}/${numpy_include} /usr/local/lib/${pydir}/${numpy_include} /usr/local/lib64/${pydir}/${numpy_include} ${prefix}/include /usr/include/${pydir} /usr/local/include /opt/numpy/include"
dnl
dnl
AC_MSG_CHECKING( for numpy include dir )
NUMPY_INCLUDE_DIR=""
for ac_dir in $EXT_GUESS ; do
    if test -f ${ac_dir}/numpy/arrayobject.h ; then
       NUMPY_INCLUDE_DIR=`(cd $ac_dir ; pwd)`
    fi
done

if test "${NUMPY_INCLUDE_DIR}" = "" ; then	
	AC_MSG_ERROR("numpy/arrayobject.h not found in ${EXT_GUESS}.")
fi

AC_MSG_RESULT($NUMPY_INCLUDE_DIR)
AC_SUBST(NUMPY_INCLUDE_DIR)

AC_MSG_CHECKING( if use of compiler specific features are allowed )

AC_ARG_ENABLE( compiler-extensions,,
	       USE_COMPILER_EXTENSIONS=$enable_compiler_features,
	       USE_COMPILER_EXTENSIONS=yes
	       )
AC_MSG_RESULT( $USE_COMPILER_EXTENSIONS )

if test "$USE_COMPILER_EXTENSIONS" != no ; then
   AC_MSG_CHECKING( for special compiler options )
   AC_SUBST(USE_COMPILER_EXTENSIONS)
   AC_DEFINE([USE_COMPILER_EXTENSIONS],1,[make use of compiler specific language features])
   CXXFLAGS="$CXXFLAGS -Wno-pmf-conversions"
   AC_SUBST(CXXFLAGS)
   AC_MSG_RESULT( $CXXFLAGS )
fi
   


AC_LANG_PUSH([C])

AC_SYS_LARGEFILE

AC_C_CONST
AC_C_INLINE
if test "$ac_cv_c_inline" != no ; then
  AC_DEFINE([HAVE_INLINE],1,[define if inline is supported])
  AC_SUBST(HAVE_INLINE)
fi

AC_TYPE_OFF_T
AC_TYPE_SIZE_T
AC_TYPE_SIGNAL
AC_C_LONG_DOUBLE
AC_LANG_POP([])

AC_CHECK_SIZEOF(int)
AC_CHECK_SIZEOF(long)
AC_CHECK_SIZEOF(long long)
AC_CHECK_SIZEOF(double)
AC_CHECK_SIZEOF(long double)


dnl Checks for library functions.
AC_TYPE_SIGNAL
AC_CHECK_FUNCS(mkdir select strdup strerror strtod uname modf modfl)
AC_CHECK_FUNCS(strdup strstr)

AC_CONFIG_FILES([pyecs/ecell3-python], [chmod +x pyecs/ecell3-python])
AC_CONFIG_FILES([pyecell/ecell3-eml2sbml], [chmod +x pyecell/ecell3-eml2sbml])
AC_CONFIG_FILES([pyecell/ecell3-sbml2eml], [chmod +x pyecell/ecell3-sbml2eml])
AC_CONFIG_FILES([pyecell/ecell3-session], [chmod +x pyecell/ecell3-session])
AC_CONFIG_FILES([pyecell/ecell3-session-gui], [chmod +x pyecell/ecell3-session-gui])
AC_CONFIG_FILES([pyecell/ecell3-session-manager],
                [chmod +x pyecell/ecell3-session-manager])
AC_CONFIG_FILES([Makefile
                 libecs/Makefile
                 libecs/tests/Makefile
                 libemc/Makefile
                 pyecs/Makefile
                 pyecs/windows/Makefile
                 pyecs/windows/ecell3-installer.iss
                 pyecs/windows/pathVariables
                 pyecell/Makefile
                 pyecell/ecell/Makefile
                 pyecell/ecell/SessionManager/Makefile
                 pyecell/ecell/analysis/Makefile
                 dm/Makefile])
AC_CONFIG_FILES([bin/Makefile
		 bin/ecell3-dmc
		 bin/ecell3-eml2em
		 bin/ecell3-em2eml],
		 [chmod +x bin/ecell3-dmc bin/ecell3-em2eml bin/ecell3-eml2em])
AC_CONFIG_FILES([session-monitor/Makefile
		 session-monitor/plugins/Makefile
                 session-monitor/config.py],
                 [chmod +x session-monitor/ecell3-session-monitor session-monitor/gecell])
AC_CONFIG_FILES([model-editor/Makefile
		 model-editor/glade/Makefile
		 model-editor/plugin/Makefile
		 model-editor/doc/Makefile],
		[chmod +x model-editor/ecell3-model-editor])
AC_CONFIG_FILES([toollauncher/Makefile
		 toollauncher/misc/Makefile],
		[chmod +x toollauncher/ecell3-toollauncher])
AC_OUTPUT
